/*
 * ************************************************************************
 *
 *  Copyright:       Robert Bosch Power Tools GmbH, 2018 - 2024
 *
 * ************************************************************************
 */

import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.InventoryHtmlReportRenderer

buildscript {
    ext {
        csmCloudCommonCoreVersion = "12.0.0"
        csmCloudCommonKafkaVersion = "4.0.0"
        csmCloudCommonMySqlVersion = "4.0.0"
        detektVersion = "${externalCatalog.versions.detekt.get()}"
        kotlinVersion = "${externalCatalog.versions.kotlin.get()}"
        mavenAzureArtifact = {
            credentials {
                username "AZURE_ARTIFACTS"
                password System.getenv("AZURE_ARTIFACTS_ENV_ACCESS_TOKEN") ?: "$azureArtifactsGradleAccessToken"
            }
            url "https://pkgs.dev.azure.com/pt-iot/_packaging/Artifacts/maven/v1"
        }
        springBootVersion = "${externalCatalog.versions.springBoot.get()}"
        springCloudVersion = "${externalCatalog.versions.springCloud.get()}"
    }
}

plugins {
    id "jacoco"
    id "maven-publish"

    alias(externalCatalog.plugins.dependencyLicenseReport)
    alias(externalCatalog.plugins.detekt)
    alias(externalCatalog.plugins.docker)
    alias(externalCatalog.plugins.gitProperties)
    alias(externalCatalog.plugins.kotlinJvm)
    alias(externalCatalog.plugins.kotlinPluginSpring)
    alias(externalCatalog.plugins.owaspDependencyCheck)
    alias(externalCatalog.plugins.sonarqube)
    alias(externalCatalog.plugins.springBoot)
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
java.toolchain.languageVersion.set(JavaLanguageVersion.of(21))

// Configurations in alphabetical order

bootJar {
    archiveBaseName = project.name
}

bootRun {
    systemProperties = System.properties
}

dependencies {
    implementation platform("com.bosch.pt:csm.cloud.dependency.bom-external:${gradle.externalBomVersion}")
    implementation platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
    implementation platform("org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion")

    detektPlugins("io.gitlab.arturbosch.detekt:detekt-formatting:$detektVersion")

    implementation("com.bosch.pt:csm.cloud.common.core:$csmCloudCommonCoreVersion")
    implementation("com.bosch.pt:csm.cloud.common.kafka:$csmCloudCommonKafkaVersion")
    implementation("com.bosch.pt:csm.cloud.common.mysql:$csmCloudCommonMySqlVersion")

    implementation("com.datadoghq:dd-trace-api")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation("io.micrometer:micrometer-core")
    implementation("io.micrometer:micrometer-registry-statsd")
    implementation("io.opentracing:opentracing-api")
    implementation("io.opentracing:opentracing-util")
    implementation("org.apache.kafka:kafka-clients")
    implementation("org.mariadb.jdbc:mariadb-java-client")

    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-jdbc")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.cloud:spring-cloud-starter-bootstrap")
    implementation("org.springframework.cloud:spring-cloud-starter-kubernetes-client-config") {
        exclude group: "com.vaadin.external.google", module: "android-json"
    }
    implementation("org.springframework.data:spring-data-commons")
    implementation("org.springframework.kafka:spring-kafka")

    testImplementation("io.mockk:mockk")
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.springframework.boot:spring-boot-starter-test")

    testImplementation("com.bosch.pt:csm.cloud.common.mysql::tests")
    testImplementation("org.testcontainers:testcontainers")
    testImplementation("org.testcontainers:junit-jupiter")
    testImplementation("org.testcontainers:mariadb")
}

dependencyCheck {
    analyzers {
        assemblyEnabled = false
        nugetconfEnabled = false
        nuspecEnabled = false
    }
    failBuildOnCVSS = 11
    formats = ["HTML", "XML"]
    // A list of configurations to exclude
    // configurations.each { println it.name }
    skipConfigurations = [
            "asciidoctor",
            "compileOnly",
            "compileClasspath",
            "detekt",
            "detektPlugins",
            "developmentOnly",
            "docker",
            "jacocoAgent",
            "jacocoAnt",
            "kaptTest",
            "testAnnotationProcessor",
            "testApi",
            "testApiDependenciesMetadata",
            "testCompile",
            "testCompileClasspath",
            "testCompileOnly",
            "testCompileOnlyDependenciesMetadata",
            "testImplementation",
            "testImplementationDependenciesMetadata",
            "testKotlinScriptDef",
            "testKotlinScriptDefExtensions",
            "testRuntime",
            "testRuntimeClasspath",
            "testRuntimeOnly",
            "testRuntimeOnlyDependenciesMetadata"
    ]
}

detekt {
    buildUponDefaultConfig = true
    config = files("config/detekt-config.yml")
    input = files("src/main/kotlin", "src/test/kotlin")
}

dockerPrepare {
    dependsOn bootJar
}

docker {
    buildArgs(["JAR_FILE": "${bootJar.archiveFileName.get()}"])
    dependsOn build
    files bootJar.archiveFile.get()
    name "ptcsmacr.azurecr.io/com.bosch.pt/csm.cloud.project.kafka.connector:$version"
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
    }
}

jar {
    // Disable plain jar creation
    enabled = false
}

licenseReport {
    excludeGroups = ["com.bosch.pt"]
    filters = [new LicenseBundleNormalizer()]
    renderers = [new InventoryHtmlReportRenderer()]
}

publishing {
    publications {
        jar(MavenPublication) {
            artifact bootJar
            artifactId = "csm.cloud.project.kafka.connector"
            from components.java
            groupId = "com.bosch.pt"
        }
    }

    repositories {
        maven mavenAzureArtifact
    }
}

repositories {
    mavenCentral()
    maven { url "https://packages.confluent.io/maven/" }
    maven mavenAzureArtifact
}

sonarqube {
    def buildReason = project.hasProperty("BuildReason") && !BuildReason.isEmpty() ? BuildReason : "Build"
    def sourceBranch = project.hasProperty("SourceBranchName") && !SourceBranchName.isEmpty() ? SourceBranchName : "${gitBranch()}-local"

    println "$buildReason for branch $sourceBranch"
    properties {
        property "sonar.projectKey", "csm.cloud.project.kafka.connector"
        property "sonar.projectName", "csm.cloud.project.kafka.connector"

        // Disable some rules on some files
        property "sonar.issue.ignore.multicriteria", "j1,j2,j3"
        // Cognitive Complexity of functions should not be too high (kotlin:S3776)
        property "sonar.issue.ignore.multicriteria.j1.ruleKey", "kotlin:S3776"
        property "sonar.issue.ignore.multicriteria.j1.resourceKey", "**/*.kt"
        // Track uses of "TO DO" tags (kotlin:S1135)
        property "sonar.issue.ignore.multicriteria.j2.ruleKey", "kotlin:S1135"
        property "sonar.issue.ignore.multicriteria.j2.resourceKey", "**/*.kt"
        // Functions should not have identical implementations (kotlin:S4144)
        property "sonar.issue.ignore.multicriteria.j3.ruleKey", "kotlin:S4144"
        property "sonar.issue.ignore.multicriteria.j3.resourceKey", "**/*.kt"

        // If the pull request was manually triggered the BuildReason is "Manual", else "PullRequest".
        // Whether the BuildReason is "PullRequest" or "Manual" we assume that a pull request has a SourceBranchName "merge".
        if ("merge" != sourceBranch) {
            property "sonar.branch.name", sourceBranch
        }
    }
}

// Workaround for now to get the spring boot tests running with gradle
sourceSets {
    main.java.srcDirs += "src/main/kotlin/"
    test.java.srcDirs += "src/test/kotlin/"
}

springBoot {
    buildInfo()
}

def isLocal = !project.hasProperty("BuildReason") || BuildReason.isEmpty()

test {
    finalizedBy jacocoTestReport
    if (isLocal)
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    useJUnitPlatform()
}

def isMergeBranch = project.hasProperty("SourceBranchName") && !SourceBranchName.isEmpty() && "merge" == SourceBranchName
def isIntegrationBranch = project.hasProperty("SourceBranchName") && !SourceBranchName.isEmpty() && "integration" == SourceBranchName
if (isLocal || isMergeBranch || isIntegrationBranch) {
    check.dependsOn dependencyCheckAnalyze
}

static def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}
version='1.0.0'
group='com.bosch.pt'
