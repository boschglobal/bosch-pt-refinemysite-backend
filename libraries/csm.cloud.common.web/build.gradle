/*
 * ************************************************************************
 *
 *  Copyright:       Robert Bosch Power Tools GmbH, 2018 - 2021
 *
 * ************************************************************************
 */

import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.InventoryHtmlReportRenderer

buildscript {
    ext {
        csmCloudCommonCoreVersion = "12.0.0"
        detektVersion = "${externalCatalog.versions.detekt.get()}"
        mavenAzureArtifact = {
            credentials {
                username "AZURE_ARTIFACTS"
                password System.getenv("AZURE_ARTIFACTS_ENV_ACCESS_TOKEN") ?: "$azureArtifactsGradleAccessToken"
            }
            url "https://pkgs.dev.azure.com/pt-iot/_packaging/Artifacts/maven/v1"
        }
        snippetsDir = file("build/generated-snippets")
        springBootVersion = "${externalCatalog.versions.springBoot.get()}"
        springCloudVersion = "${externalCatalog.versions.springCloud.get()}"
    }
}

plugins {
    id "com.bosch.pt.csm.cloud.translations" version "4.0.0"
    id "java-library"
    id "maven-publish"

    alias(externalCatalog.plugins.dependencyLicenseReport)
    alias(externalCatalog.plugins.detekt)
    alias(externalCatalog.plugins.kotlinJvm)
    alias(externalCatalog.plugins.kotlinPluginSpring)
    alias(externalCatalog.plugins.release)
    alias(externalCatalog.plugins.sonarqube)
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
java.toolchain.languageVersion.set(JavaLanguageVersion.of(21))

//Read version number from version.txt
String versionFromFile = file("version.txt").text.trim()
String versionNumber = versionFromFile.substring(versionFromFile.indexOf("=") + 1)
//If version number is specified by -Pversion=... take it, otherwise take number from file
version = "unspecified".equals(version) ? versionNumber : version

// Task registrations and definitions

configurations {
    tests
}

task testJar(type: Jar) {
    archiveClassifier = "tests"
    dependsOn testClasses
    from sourceSets.test.output.classesDirs
}

task sourcesJar(type: Jar) {
    archiveClassifier = "sources"
    dependsOn testClasses
    from sourceSets.main.allSource + sourceSets.test.allSource
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
    dependsOn generateMessagesKeyKotlin
}

compileKotlin {
    dependsOn generateMessagesKeyKotlin
}

// Configurations in alphabetical order

artifacts {
    archives testJar
    archives sourcesJar
    tests testJar
}

dependencies {
    implementation platform("com.bosch.pt:csm.cloud.dependency.bom-external:${gradle.externalBomVersion}")
    implementation platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
    implementation platform("org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion")

    api("com.bosch.pt:csm.cloud.common.core:$csmCloudCommonCoreVersion")

    detektPlugins("io.gitlab.arturbosch.detekt:detekt-formatting:$detektVersion")

    implementation("com.datadoghq:dd-trace-api")
    implementation("org.aspectj:aspectjweaver")
    implementation("org.jetbrains.kotlin:kotlin-stdlib")

    implementation("org.springframework.boot:spring-boot-configuration-processor")
    implementation("org.springframework.boot:spring-boot-starter")
    implementation("org.springframework.boot:spring-boot-starter-hateoas")
    implementation("org.springframework.boot:spring-boot-starter-oauth2-resource-server")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-actuator-autoconfigure")

    implementation("org.springframework.data:spring-data-commons")


    testImplementation("io.mockk:mockk")
    testImplementation("org.assertj:assertj-core")
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
}

detekt {
    buildUponDefaultConfig = true
    config = files("config/detekt-config.yml")
    input = files("src/main/kotlin", "src/test/kotlin")
}

generateMessagesKeyKotlin {
    inputFile file("src/main/resources/i18n/common/web/messages.properties")
    outputFile file("src/main/kotlin/com/bosch/pt/csm/cloud/common/i18n/CommonWebKey.kt")
}

licenseReport {
    excludeGroups = ["com.bosch.pt"]
    filters = [new LicenseBundleNormalizer()]
    renderers = [new InventoryHtmlReportRenderer()]
}

publishing {
    publications {
        jar(MavenPublication) {
            artifact sourcesJar
            artifact testJar
            artifactId = "csm.cloud.common.web"
            from components.java
            groupId = "com.bosch.pt"
        }
    }

    repositories {
        maven mavenAzureArtifact
    }
}

release {
    buildTasks = []
    newVersionCommitMessage = "New snapshot version:"
    preTagCommitMessage = "Release:"
    versionPropertyFile = "version.txt"
    git {
        requireBranch.set("master")
    }
}

repositories {
    mavenCentral()
    maven mavenAzureArtifact
}

// Add src/main/kotlin as source set to fix issues with compileJava task.
// Otherwise no files are found to compile and the the build fails with
// The error message: > error: no source files
sourceSets {
    main.java.srcDirs += "src/main/kotlin/"
    test.java.srcDirs += "src/test/kotlin/"
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}
group='com.bosch.pt'
